plugins {
  id 'base'
  id 'maven-publish'
  id 'edu.ucar.unidata.site.jekyll' version '0.0.2'
}

def readVersionFromGemspec(String gemspec) {
  def version
  new File(gemspec).withReader('UTF-8') { reader ->
    def line
    while ((line = reader.readLine()) != null) {
      if (line.contains('spec.version')) {
        version = "${line.split('=').last() as String}".replace("\"", "").replace("'", "")
        // remove single and double quotes
        version = version.replace("\"", "")
        version = version.replace("'", "")
        version = version.trim()
        break
      }
    }
  }
  version as String
}

// the version of the gem files - read these from the gemspec files.
def themeVersion = readVersionFromGemspec('unidata-jekyll-theme.gemspec')
def pluginVersion = readVersionFromGemspec('unidata-jekyll-plugins.gemspec')

// use gemjar without the unidata jekyll theme and plugin gems bundled in
configurations.gemjar {
  resolutionStrategy.dependencySubstitution {
    substitute(module("edu.ucar.unidata.site:jekyll-gems:${themeVersion}")).
        using module("edu.ucar.unidata.site:jekyll-gems-minimum:${themeVersion}")
  }
}

unidataJekyll {
  sourceDirectory = file('.')
}

def gemNameTheme = "unidata-jekyll-theme-${themeVersion}.gem"
def gemNamePlugin = "unidata-jekyll-plugins-${pluginVersion}.gem"

tasks.named('wrapper') {
  distributionType = Wrapper.DistributionType.ALL
  // when upgrading, make sure we are using a compatible version of spock-core for testing.
  gradleVersion = '6.8.2'
}

ext {
  NEXUS_USERNAME_KEY = 'nexus.username'
  NEXUS_PASSWORD_KEY = 'nexus.password'
}

String getPropertyOrFailBuild(String key) {
  if (!hasProperty(key)) {
    throw new GradleException("You must define the '$key' property.")
  } else {
    property(key) as String
  }
}

def gemFileTheme = file("${gemNameTheme}")

def gemArtifactTheme = artifacts.add('archives', gemFileTheme) {
  type 'gem'
  extension 'gem'
}

def gemFilePlugin = file("${gemNamePlugin}")

def gemArtifactPlugin = artifacts.add('archives', gemFilePlugin) {
  type 'gem'
  extension 'gem'
}

publishing {
  repositories {
    maven {
      name = 'releases'
      url = 'https://artifacts.unidata.ucar.edu/repository/unidata-releases/'
    }
  }

  publications {
    themeGem(MavenPublication) {
      groupId = 'rubygems'
      artifactId = 'unidata-jekyll-theme'
      version = "${themeVersion}"

      artifact gemArtifactTheme
    }
    pluginGem(MavenPublication) {
      groupId = 'rubygems'
      artifactId = 'unidata-jekyll-plugins'
      version = "${pluginVersion}"

      artifact gemArtifactPlugin
    }
  }
}

// short cut for publishing a single gem
tasks.create('publishPlugin').dependsOn('publishPluginGemPublicationToReleasesRepository').group('publishing')
tasks.create('publishTheme').dependsOn('publishThemeGemPublicationToReleasesRepository').group('publishing')

// The "publish" tasks require credentials for our Nexus server, which they look for in Gradle properties.
// If those properties (i.e. NEXUS_USERNAME_KEY and NEXUS_PASSWORD_KEY) haven't been provided, the build will fail.
// Therefore, we only want to configure credentials when a "publish" task is part of the execution plan. Otherwise,
// unavailable credentials could cause a build to fail even if we aren't doing any publishing. The TaskExecutionGraph
// allows us to do that.
gradle.taskGraph.whenReady {TaskExecutionGraph taskGraph ->
  // This won't find any publishToMavenLocal tasks. Those are of type PublishToMavenLocal
  Collection<Task> mavenPublishTasks = taskGraph.allTasks.findAll {
    it instanceof PublishToMavenRepository
  }

  mavenPublishTasks.each {
    it.repository.credentials.with {
      username = getPropertyOrFailBuild NEXUS_USERNAME_KEY
      password = getPropertyOrFailBuild NEXUS_PASSWORD_KEY
    }
  }
}
